apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)


def paramsString = project.gradle.startParameter.taskNames.toString()
def currentlyBuildingRelease = paramsString.contains('Release')
def currentlyRunningTest = paramsString.contains('assembleDebugAndroidTest')
def libraryVersion = rootProject.file('bin/prepare.files/version').text//'1.1.5'


println "android gradle params: " + paramsString.replace(",","\n") + "\n\n"

android {
    compileSdkVersion 24
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                if (currentlyBuildingRelease) {
                    arguments '-DANDROID_RELEASE=true'
                } else if (currentlyRunningTest) {
                    arguments '-DANDROID_TESTING=true'
                } else {
                    arguments '-DANDROID_TESTING=false'
                }
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation 'com.android.volley:volley:1.0.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:24.2.0'
    implementation 'com.android.support:design:24.2.0'
    testImplementation 'junit:junit:4.12'
}

publish {

    userOrg = 'the-mac'
    groupId = 'us.the.mac'
    artifactId = 'AndroidJniHelpers'
    publishVersion = libraryVersion
    website = 'https://github.com/the-mac/AndroidJniHelpers'
    desc = 'Tools for writing secure Android/JNI code, based upon Spotify\'s Jni Helpers Library.'

}

tasks.withType(Javadoc).all {
    enabled = false
}