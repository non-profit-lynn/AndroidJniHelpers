apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'the-mac'
version '1.1.5'
description = 'Small update to bintray release'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId this.group
            artifactId 'AndroidJNIHelpers'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'AndroidJNIHelpers'
        name = 'AndroidJNIHelpers'
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/the-mac/AndroidJniHelpers.git'
        dryRun = true
        version {
            name = this.version
            vcsTag = this.version
            desc = this.description
            released = new Date()
        }
    }
}

def bintrayString = this.bintray.pkg.version.toString()
println "android gradle bintrayString: " + bintrayString.replace(",","\n") + "\n\n"